/*
 * entry.S - Entry point to system mode from user mode
 */

#include <asm.h>
#include <segment.h>
#include <errno.h>

/**************************************************/
/**** Save & Restore ******************************/
/**                                              **/
/** When we change to privilege level 0 (kernel) **/
/** (through an interrupt, a system call, an     **/
/** exception ...) we must save the state of the **/
/** currently running task (save).               **/
/**                                              **/
/** Stack layout in 'systemCall':                **/
/**                                              **/
/**   0(%esp) - %ebx    \                        **/
/**   4(%esp) - %ecx     |                       **/
/**   8(%esp) - %edx     |                       **/
/**   C(%esp) - %esi     | Register saved        **/
/**  10(%esp) - %edi     |  by 'save'            **/
/**  14(%esp) - %ebp     |                       **/
/**  18(%esp) - %eax     |                       **/
/**  1C(%esp) - %ds      |                       **/
/**  20(%esp) - %es      |                       **/
/**  24(%esp) - %fs      |                       **/
/**  28(%esp) - %gs     /                        **/
/**  2C(%esp) - %eip    \                        **/
/**  30(%esp) - %cs      |                       **/
/**  34(%esp) - %eflags  |  Return context saved **/
/**  38(%esp) - %oldesp  |   by the processor.   **/
/**  3C(%esp) - %oldss  /                        **/
/**                                              **/
/**************************************************/

#define SAVE_ALL \
      pushl %gs; \
      pushl %fs; \
      pushl %es; \
      pushl %ds; \
      pushl %eax; \
      pushl %ebp; \
      pushl %edi; \
      pushl %esi; \
      pushl %edx; \
      pushl %ecx; \
      pushl %ebx; \
      movl $__KERNEL_DS, %edx;    \
      movl %edx, %ds;           \
      movl %edx, %es

#define MODIFY_EAX \
      movl %eax, 0x18(%esp)

#define RESTORE_ALL \
      popl %ebx; \
      popl %ecx; \
      popl %edx; \
      popl %esi; \
      popl %edi; \
      popl %ebp; \
      popl %eax; \
      popl %ds; \
      popl %es; \
      popl %fs; \
      popl %gs

#define EOI \
      movb $0x20, %al ; \
      outb %al, $0x20


ENTRY(keyboard_handler)
      SAVE_ALL
      EOI
      call keyboard_routine
      RESTORE_ALL
      iret

ENTRY(clock_handler)
      SAVE_ALL
      EOI
      call clock_routine
      RESTORE_ALL
      iret

/*
  As we do not intend to return from this exception, we could avoid saving the
  registers (omit the SAVE_ALL). With this, we would also not need to copy
  push %eip to the stack again.
  We are passing to the function not only the %eip of the instruction that
  caused the fault but also the attempted access direction
*/
ENTRY(custom_page_fault_handler)
      SAVE_ALL
      push 0x30(%esp)
      mov %cr2, %eax
      push %eax
      call custom_page_fault_routine
      addl $8, %esp
      RESTORE_ALL
      addl $4, %esp /* Remove errror code */
      iret
      
ENTRY(syscall_handler)
      SAVE_ALL
      cmpl $MAX_SYSCALL, %eax
      jnb syscall_err
      call *sys_call_table(,%eax, 4)
      jmp syscall_end
syscall_err:
      movl $-ENOSYS, %eax
syscall_end:
      MODIFY_EAX
      RESTORE_ALL
      iret

ENTRY(syscall_handler_sysenter)
      push $__USER_DS
      push %ebp
      pushfl
      push $__USER_CS
      push 4(%ebp)
      SAVE_ALL
      cmpl $MAX_SYSCALL, %eax
      jnb sysenter_err
      call *sys_call_table(,%eax, 4)
      jmp sysenter_end
sysenter_err:
      movl $-ENOSYS, %eax
sysenter_end: 
      MODIFY_EAX
      RESTORE_ALL
      movl (%esp), %edx
      movl 12(%esp), %ecx
      sti
      sysexit
      

